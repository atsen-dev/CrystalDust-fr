const OBJ_EVT_MOM = 1

mapscripts NewBarkTown_PlayersHouse_1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (var(VAR_PLAYER_HOUSE_STATE) != 0)
        {
            gettime
            if (var(VAR_0x8002) == TIME_MORNING)
            {
                setobjectxyperm(OBJ_EVT_MOM, 2, 2)
                setobjectmovementtype(OBJ_EVT_MOM, MOVEMENT_TYPE_FACE_UP)
            }
            elif (var(VAR_0x8002) == TIME_NIGHT)
            {
                setobjectxyperm(OBJ_EVT_MOM, 1, 2)
                setobjectmovementtype(OBJ_EVT_MOM, MOVEMENT_TYPE_FACE_UP)
            }
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_PLAYER_HOUSE_STATE, 0 : NewBarkTown_PlayersHouse_1F_MomEncounter
    ]
}

script NewBarkTown_PlayersHouse_1F_MomEncounter {
    lockall
    textcolor(MSG_COLOR_RED)
    playse(SE_PIN)
    applymovement(OBJ_EVT_MOM, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(OBJ_EVT_MOM, Common_Movement_Delay48)
    waitmovement(0)
    playbgm(MUS_MOMS_THEME, 0)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFastestDown)
    applymovement(OBJ_EVT_MOM, NewBarkTown_PlayersHouse_1F_MomWalkToPlayer)
    waitmovement(0)
    delay(20)
    msgbox("MAMAN: Ah, {PLAYER}…!\p"
           "Notre voisin, le PROF. ORME, te/ncherchait.\p"
           "Il voulait que tu fasses quelque chose\n"
           "pour lui.\p"
           "Oh, J'allais oublier! Ton MATOS {POKEMON}\n"
           "est revenu de chez le réparateur.\l"
           "Tiens!")
    textcolor(MSG_COLOR_PREV)
    playfanfare(MUS_OBTAIN_ITEM)
    message("{PLAYER} recoit le {POKE}GEAR.")
    waitfanfare
    setflag(FLAG_SYS_POKEGEAR_GET)
    setflag(FLAG_PHONE_CARD_MOM)
    textcolor(MSG_COLOR_RED)
    msgbox("le MATOS {POKEMON}, ou simplement {POKE}GEAR...\n"
           "est primordial si tu veux devenir\l"
           "dresseur.\p"
           "Oh, le jour de la semaine n'est pas\n"
           "paramétré.\n"
           "N'oublie pas ca!")
    call(NewBarkTown_PlayersHouse_1F_MomWhatDayOfWeek)
    call(NewBarkTown_PlayersHouse_1F_MomDaylightSaving)
    msgbox("Reviens à la maison pour le changement d'heure.\p"
           "Par ailleurs sais-tu comment\n"
           "utiliser le TELEPHONE?", MSGBOX_YESNO)

    if (var(VAR_RESULT) == NO)
    {
        msgbox("I'll read the instructions.\p"
               "“Turn the {POKE}GEAR on and select\n"
               "the PHONE icon by scrolling to it.\p"
               "“Phone numbers are stored in memory.\n"
               "Just choose who you want to call.”\p"
               "Gee, isn't that convenient?")
    }
    else
    {
        msgbox("As-tu allumé le {POKE}GEAR et\n"
               "séléctionné l'icone TELEPHONE?\p"
               "“Les numéros de téléphones sont enregistré\n"
               "dans la mémoire.\n"
               "Choisis le nom de celui que tu veux appeler.”\p"
               "C'est pas trop compliqué, non?")
    }

    closemessage
    savebgm(MUS_DUMMY)
    fadedefaultbgm
    applymovement(OBJ_EVT_MOM, NewBarkTown_PlayersHouse_1F_MomWalkAwayFromPlayer)
    waitmovement(0)
    setvar(VAR_PLAYER_HOUSE_STATE, 1)
    setrespawn(HEAL_LOCATION_NEW_BARK_TOWN)   // TODO: This should really be for player's room 2F, or do the FR approach
    releaseall
}

script NewBarkTown_PlayersHouse_1F_MomWhatDayOfWeek {
    message("What day is it?")
    waitmessage
    setvar(VAR_0x8004, SCROLL_MULTI_DAY_OF_WEEK)
    setvar(VAR_0x8005, 0)
    special(ShowScrollableMultichoice)
    waitstate
    copyvar(VAR_0x8004, VAR_RESULT)
    special(CopyDayOfWeekStringToVar1)
    msgbox("{STR_VAR_1}, is it?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == NO)
    {
        goto(NewBarkTown_PlayersHouse_1F_MomWhatDayOfWeek)
    }
    special(SetDayOfWeek)
}

script NewBarkTown_PlayersHouse_1F_MomDaylightSaving {
    msgbox("Is it Daylight Saving Time now?", MSGBOX_YESNO)
    special(WriteCurrentTimeStringToStrVar1)

    if (var(VAR_RESULT) == NO)
    {
        copyvar(VAR_0x8004, VAR_RESULT)
        special(SetInitialDSTMode)
        msgbox("{STR_VAR_1}, is that correct?", MSGBOX_YESNO)
    }
    else
    {
        copyvar(VAR_0x8004, VAR_RESULT)
        special(SetInitialDSTMode)
        msgbox("{STR_VAR_1} DST, is that correct?", MSGBOX_YESNO)
    }
    
    if (var(VAR_RESULT) == NO)
    {
        goto(NewBarkTown_PlayersHouse_1F_MomDaylightSaving)
    }
}

movement NewBarkTown_PlayersHouse_1F_MomWalkToPlayer {
    walk_up
    walk_right
    walk_up
}

movement NewBarkTown_PlayersHouse_1F_MomWalkAwayFromPlayer {
    walk_down
    walk_left
    walk_down
    walk_in_place_fastest_left
}

script NewBarkTown_PlayersHouse_1F_MomTalk {
    lock
    faceplayer
    if (flag(FLAG_PLAYER_MUST_LEAVE_HOUSE_TO_BANK_WITH_MOM))
    {
        msgbox("MAMAN: {PLAYER}, do it!\n"
               "I'm behind you all the way!")
    }
    elif (flag(FLAG_TALKED_TO_MOM_AFTER_MYSTERY_EGG_QUEST))
    {
        msgbox("MAMAN: Salut! Bienvenue à la maison!\n"
               "You're trying very hard, I see.\p"
               "I've kept your room tidy…\n"
               "Or is this about your money?", MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES)
        {
            goto(NewBarkTown_PlayersHouse_1F_EventScript_MomBank)
        }
        else
        {
            gettime
            special(GetDSTMode)
            if (var(VAR_RESULT) == TRUE)
            {
                if (var(VAR_0x8000) == 0) // 12 AM
                {
                    call(NewBarkTown_PlayersHouse_1F_EventScript_MomLostBooklet)
                }
                else
                {
                    msgbox("Is Daylight Saving Time over?", MSGBOX_YESNO)
                    if (var(VAR_RESULT) == YES)
                    {
                        special(SwitchDSTMode)
                        msgbox("I put the clock back one hour.")
                    }
                }
            }
            else
            {
                if (var(VAR_0x8000) == 23) // 11 PM
                {
                    call(NewBarkTown_PlayersHouse_1F_EventScript_MomLostBooklet)
                }
                else
                {
                    msgbox("Do you want to switch to Daylight\n"
                           "Saving Time?", MSGBOX_YESNO)
                    if (var(VAR_RESULT) == YES)
                    {
                        special(SwitchDSTMode)
                        msgbox("I set the clock forward by one hour.")
                    }
                }
            }
        }
        msgbox(NewBarkTown_PlayersHouse_1F_EventScript_MomJustDoWhatYouCan)
    }
    elif (flag(FLAG_GAVE_MYSTERY_EGG_TO_ELM))
    {
        setflag(FLAG_TALKED_TO_MOM_AFTER_MYSTERY_EGG_QUEST)
        setflag(FLAG_PLAYER_MUST_LEAVE_HOUSE_TO_BANK_WITH_MOM)
        setvar(VAR_ROUTE31_STATE, 1)
        msgbox("MAMAN: Wow, that's a cute {POKEMON}.\n"
               "Where did you get it?\p"
               "…\p"
               "So, you're leaving on an adventure…\p"
               "Okay! I'll help too.\n"
               "But what can I do for you?\p"
               "I know!\n"
               "I'll save money for you.\p"
               "On a long journey, money is very\n"
               "important.\p"
               "Do you want me to save your money?", MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES)
        {
            setflag(FLAG_SYS_MOM_BANKING_ENABLED)
            msgbox("Okay, I'll take care of your money.\n"
                   "………………")
        }
        msgbox("Be careful.\p"
               "{POKEMON} are your friends.\n"
               "You need to work as a team.\p"
               "Now, go on!")
    }
    elif (flag(FLAG_SYS_POKEMON_GET))
    {
        msgbox("MAMAN: So, what was PROF. ORME's\n"
               "errand?\p"
               "…\n"
               "That does sound challenging.\p"
               "MaisBut, tu devrais être fier que\n"
               "les gens comptent sur toi.")
    }
    else
    {
        msgbox("MAMAN: PROF. ORME t'attend.\n"
               "Dépêche-toi, mon poussin!")
    }
    release
}

text NewBarkTown_PlayersHouse_1F_EventScript_MomJustDoWhatYouCan {
    "Just do what you can."
}

script NewBarkTown_PlayersHouse_1F_EventScript_MomBank {
    message("Que veux-tu faire?")
    waitmessage
    multichoice(0, 0, MULTI_MOM_BANK, FALSE)
    switch (var(VAR_RESULT))
    {
    case 0:
        // take
        call(NewBarkTown_PlayersHouse_1F_EventScript_MomBankWithdraw)
    case 1:
        // deposit
        call(NewBarkTown_PlayersHouse_1F_EventScript_MomBankDeposit)
    case 2:
        msgbox("Do you want to save some money?", MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES)
        {
            setflag(FLAG_SYS_MOM_BANKING_ENABLED)
            msgbox("Okay, I'll save your money.\n"
                   "Trust me!\p"
                   "{PLAYER}, stick with it!")
        }
        else
        {
            clearflag(FLAG_SYS_MOM_BANKING_ENABLED)
            msgbox(NewBarkTown_PlayersHouse_1F_EventScript_MomJustDoWhatYouCan)
        }
    default:
        msgbox(NewBarkTown_PlayersHouse_1F_EventScript_MomJustDoWhatYouCan)
    }
    release
}

script NewBarkTown_PlayersHouse_1F_EventScript_MomBankWithdraw {
    setvar(VAR_0x8004, 0)
    special(MomBank)
    waitstate
    switch (var(VAR_RESULT))
    {
    case 0:
        // withdraw success
        playse(SE_SHOP)
        waitse
        msgbox("{PLAYER}, don't give up!")
    case 1:
        // not enough in bank
        msgbox("You haven't saved that much.")
        goto(NewBarkTown_PlayersHouse_1F_EventScript_MomBankWithdraw)
    case 2:
        // too much in wallet
        msgbox("You can't take that much.")
        goto(NewBarkTown_PlayersHouse_1F_EventScript_MomBankWithdraw)
    default:
        msgbox(NewBarkTown_PlayersHouse_1F_EventScript_MomJustDoWhatYouCan)
    }
}

script NewBarkTown_PlayersHouse_1F_EventScript_MomBankDeposit {
    setvar(VAR_0x8004, 1)
    special(MomBank)
    waitstate
    switch (var(VAR_RESULT))
    {
    case 0:
        // deposit success
        playse(SE_SHOP)
        waitse
        msgbox("Your money's safe here!\n"
                "Get going!")
    case 1:
        // not enough in wallet
        msgbox("You don't have that much.")
        goto(NewBarkTown_PlayersHouse_1F_EventScript_MomBankDeposit)
    case 2:
        // too much in bank
        msgbox("You can't save that much.")
        goto(NewBarkTown_PlayersHouse_1F_EventScript_MomBankDeposit)
    default:
        msgbox(NewBarkTown_PlayersHouse_1F_EventScript_MomJustDoWhatYouCan)
    }
}

script NewBarkTown_PlayersHouse_1F_EventScript_MomLostBooklet {
    msgbox("Veux-tu ajuster la montre pour\n"
           "l'Heure d'été?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES)
    {
        msgbox("Je pense que j'ai perdu le manuel\n"
               "d'instructions du {POKE}GEAR…\p"
               "Il faut que je le retrouve.\n"
               "Reviens à nouveau dans quelques temps.")
    }
}

script NewBarkTown_PlayersHouse_1F_NeighborTalk {
    lock
    faceplayer
    gettime
    if (var(VAR_0x8002) == TIME_MORNING)
    {
        msgbox("Good morning, {PLAYER}!\n"
               "Je suis de passage!\p", MSGBOX_CONTINUE)
    }
    elif (var(VAR_0x8002) == TIME_DAY)
    {
        msgbox("Bonjour, {PLAYER}!\n"
               "Je suis de passage!\p", MSGBOX_CONTINUE)
    }
    elif (var(VAR_0x8002) == TIME_NIGHT)
    {
        msgbox("Bonsoir, {PLAYER}!\n"
               "Je suis de passage!\p", MSGBOX_CONTINUE)
    }
    msgbox("{PLAYER}, es-tu au courant?\p"
           "Ma fille veut devenir l'assistante du\n"
           "PROF. ORME.\l"
           "Elle adoooore les {POKEMON}!")
    closemessage
    applymovement(VAR_LAST_TALKED, Common_Movement_WalkInPlaceFastestRight)
    waitmovement(0)
    release
}

script NewBarkTown_PlayersHouse_1F_TV {
    msgbox("Un film à la TV:\n"
           "On Se Calme Et On\l"
           "Boit Frais à Saint-Tropez…\p"
           "Ca a l'air trop fort!", MSGBOX_SIGN)
}

script NewBarkTown_PlayersHouse_1F_Stove {
    lock
    random(1000)
    if (var(VAR_RESULT) == 0)
    {
        msgbox("A ADEPT ARRANGING FOR MOTHER\n"
               "VOLCANO BAKEMEAT.")
    }
    else
    {
        msgbox("C'est la spécialité de Maman!\n"
               "Un bon BURGER style CRAMOIS'ILE!")
    }
    release
}

script NewBarkTown_PlayersHouse_1F_Sink {
    msgbox("L'evier est nickel de chez nickel.\n"
           "Maman est assez maniaque.", MSGBOX_SIGN)
}

script NewBarkTown_PlayersHouse_1F_Fridge {
    msgbox("Voyons voir ce qui se trame\n"
           "dans le frigo…\p"
           "De l'EAU FRAICHE et de la\n"
           "bonne LIMONADE!", MSGBOX_SIGN)
}
